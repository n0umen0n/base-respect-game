// SPDX-License-Identifier: MIT
pragma solidity 0.8.28;

import {ECDSA} from "solady/utils/ECDSA.sol";
import {Initializable} from "solady/utils/Initializable.sol";

import {IPartner} from "./interfaces/IPartner.sol";
import {MessageLib} from "./libraries/MessageLib.sol";
import {Pubkey} from "./libraries/SVMLib.sol";
import {VerificationLib} from "./libraries/VerificationLib.sol";

import {Bridge} from "./Bridge.sol";

/// @title BridgeValidator
///
/// @notice A validator contract to be used during the Stage 0 phase of Base Bridge. This will likely later be replaced
///         by `CrossL2Inbox` from the OP Stack.
contract BridgeValidator is Initializable {
    using ECDSA for bytes32;

    /// @notice Container for data used to derive a unique `messageHash` for registration.
    struct SignedMessage {
        /// @notice Hash of the inner message payload (excluding nonce and gas limits).
        bytes32 innerMessageHash;
        /// @notice SVM/Solana pubkey associated with the outgoing message for this registration.
        Pubkey outgoingMessagePubkey;
    }

    //////////////////////////////////////////////////////////////
    ///                       Constants                        ///
    //////////////////////////////////////////////////////////////

    /// @notice The max allowed partner validator threshold
    uint256 public constant MAX_PARTNER_VALIDATOR_THRESHOLD = 5;

    /// @notice Guardian role bit used by the `Bridge` contract for privileged actions on this contract.
    uint256 public constant GUARDIAN_ROLE = 1 << 0;

    /// @notice Address of the Base Bridge contract. Used for authenticating guardian roles
    address public immutable BRIDGE;

    /// @notice Address of the contract holding the partner validator set
    address public immutable PARTNER_VALIDATORS;

    /// @notice A bit to be used in bitshift operations
    uint256 private constant _BIT = 1;

    //////////////////////////////////////////////////////////////
    ///                       Storage                          ///
    //////////////////////////////////////////////////////////////

    /// @notice Required number of partner signatures
    uint256 public partnerValidatorThreshold;

    /// @notice The next expected nonce to be received in `registerMessages`
    uint256 public nextNonce;

    /// @notice A mapping of pre-validated valid messages. Each pre-validated message corresponds to a message sent
    ///         from Solana.
    mapping(bytes32 messageHash => bool isValid) public validMessages;

    //////////////////////////////////////////////////////////////
    ///                       Events                           ///
    //////////////////////////////////////////////////////////////

    /// @notice Emitted when a single message is registered (pre-validated).
    ///
    /// @param messageHash           The pre-validated message hash (derived from the inner message hash and an
    ///                              incremental nonce) corresponding to an `IncomingMessage` in the `Bridge` contract.
    /// @param outgoingMessagePubkey The SVM/Solana pubkey associated with the outgoing message for this registration.
    event MessageRegistered(bytes32 indexed messageHash, Pubkey indexed outgoingMessagePubkey);

    /// @notice Emitted when the partner validator threshold is updated.
    ///
    /// @param oldThreshold The previous partner validator threshold.
    /// @param newThreshold The new partner validator threshold.
    event PartnerThresholdUpdated(uint256 oldThreshold, uint256 newThreshold);

    //////////////////////////////////////////////////////////////
    ///                       Errors                           ///
    //////////////////////////////////////////////////////////////

    /// @notice Thrown when the provided `validatorSigs` byte string length is not a multiple of 65
    error InvalidSignatureLength();

    /// @notice Thrown when the required amount of Base signatures is not included with a `registerMessages` call
    error BaseThresholdNotMet();

    /// @notice Thrown when the required amount of partner signatures is not included with a `registerMessages` call
    error PartnerThresholdNotMet();

    /// @notice Thrown when a zero address is detected
    error ZeroAddress();

    /// @notice Thrown when the partner validator threshold is higher than number of validators
    error ThresholdTooHigh();

    /// @notice Thrown when the caller of a protected function is not a Base Bridge guardian
    error CallerNotGuardian();

    /// @notice Thrown when a duplicate partner validator is detected during signature verification
    error DuplicateSigner();

    /// @notice Thrown when the recovered signers are not sorted
    error UnsortedSigners();

    /// @notice Thrown when attempting to register an empty batch of messages
    error NoMessages();

    /// @notice Thrown when the Bridge is paused
    error Paused();

    //////////////////////////////////////////////////////////////
    ///                       Modifiers                        ///
    //////////////////////////////////////////////////////////////

    /// @dev Restricts function to when the Bridge is not paused
    modifier whenNotPaused() {
        require(!Bridge(BRIDGE).paused(), Paused());
        _;
    }

    //////////////////////////////////////////////////////////////
    ///                       Public Functions                 ///
    //////////////////////////////////////////////////////////////

    /// @notice Deploys the BridgeValidator contract with configuration for partner signatures and the `Bridge` address.
    ///
    /// @dev Reverts with `ZeroAddress()` if `bridge` is the zero address.
    ///
    /// @param bridgeAddress     The address of the `Bridge` contract used to check guardian roles.
    /// @param partnerValidators Address of the contract holding the partner validator set
    constructor(address bridgeAddress, address partnerValidators) {
        require(bridgeAddress != address(0), ZeroAddress());
        require(partnerValidators != address(0), ZeroAddress());

        partnerValidatorThreshold = type(uint256).max;
        VerificationLib.getVerificationLibStorage().threshold = type(uint128).max;

        BRIDGE = bridgeAddress;
        PARTNER_VALIDATORS = partnerValidators;
        _disableInitializers();
    }

    /// @notice Initializes Base validator set and threshold.
    ///
    /// @dev Callable only once due to `initializer` modifier.
    ///
    /// @param baseValidators The initial list of Base validators.
    /// @param baseThreshold  The minimum number of Base validator signatures required.
    /// @param partnerThreshold The minimum number of partner validator signatures required.
    function initialize(address[] calldata baseValidators, uint128 baseThreshold, uint256 partnerThreshold)
        external
        initializer
    {
        VerificationLib.initialize(baseValidators, baseThreshold);

        require(partnerThreshold <= MAX_PARTNER_VALIDATOR_THRESHOLD, ThresholdTooHigh());
        partnerValidatorThreshold = partnerThreshold;
    }

    /// @notice Pre-validates a batch of Solana â†’ Base messages.
    ///
    /// @param signedMessages An array of `SignedMessage` structs. For each entry, the `messageHash` is computed as
    ///                       `MessageLib.getMessageHash(nonce, outgoingMessagePubkey, innerMessageHash)` where `nonce`
    ///                       increments monotonically from `nextNonce`.
    /// @param validatorSigs  A concatenated bytes array of signatures over the EIP-191 `eth_sign` digest of
    ///                       `abi.encode(messageHashes)`, provided in strictly ascending order by signer address.
    ///                       Must include at least `getBaseThreshold()` Base validator signatures. If
    ///                       `partnerValidatorThreshold > 0`, must also include at least `partnerValidatorThreshold`
    ///                       partner validator signatures.
    function registerMessages(SignedMessage[] calldata signedMessages, bytes calldata validatorSigs)
        external
        whenNotPaused
    {
        uint256 len = signedMessages.length;
        if (len == 0) revert NoMessages();

        bytes32[] memory messageHashes = new bytes32[](len);
        uint256 currentNonce = nextNonce;

        for (uint256 i; i < len; i++) {
            messageHashes[i] = MessageLib.getMessageHash(
                currentNonce++, signedMessages[i].outgoingMessagePubkey, signedMessages[i].innerMessageHash
            );
        }

        _validateSigs({messageHashes: messageHashes, sigData: validatorSigs});

        for (uint256 i; i < len; i++) {
            validMessages[messageHashes[i]] = true;
            emit MessageRegistered(messageHashes[i], signedMessages[i].outgoingMessagePubkey);
        }

        nextNonce = currentNonce;
    }

    /// @notice Gets the current Base signature threshold.
    ///
    /// @return The current Base signature threshold.
    function getBaseThreshold() external view returns (uint128) {
        return VerificationLib.getBaseThreshold();
    }

    /// @notice Gets the registered Base validator count
    function getBaseValidatorCount() external view returns (uint256) {
        return VerificationLib.getBaseValidatorCount();
    }

    /// @notice Returns true if `validator` is a registered Base validator address
    function isBaseValidator(address validator) external view returns (bool) {
        return VerificationLib.isBaseValidator(validator);
    }

    //////////////////////////////////////////////////////////////
    ///                    Private Functions                   ///
    //////////////////////////////////////////////////////////////

    /// @dev Verifies that the provided signatures satisfy Base and partner thresholds for `messageHashes`.
    ///
    /// @param messageHashes The derived message hashes (inner hash + nonce) for the batch.
    /// @param sigData       Concatenated signatures over `toEthSignedMessageHash(abi.encode(messageHashes))`.
    function _validateSigs(bytes32[] memory messageHashes, bytes calldata sigData) private view {
        address[] memory recoveredSigners = _getSignersFromSigs(messageHashes, sigData);
        require(_countBaseSigners(recoveredSigners) >= VerificationLib.getBaseThreshold(), BaseThresholdNotMet());

        uint256 partnerValidatorThreshold_ = partnerValidatorThreshold;
        if (partnerValidatorThreshold_ > 0) {
            IPartner.Signer[] memory partnerValidators = IPartner(PARTNER_VALIDATORS).getSigners();
            require(
                _countPartnerSigners(partnerValidators, recoveredSigners) >= partnerValidatorThreshold_,
                PartnerThresholdNotMet()
            );
        }
    }

    function _getSignersFromSigs(bytes32[] memory messageHashes, bytes calldata sigData)
        private
        view
        returns (address[] memory)
    {
        // Check that the provided signature data is a multiple of the valid sig length
        require(sigData.length % VerificationLib.SIGNATURE_LENGTH_THRESHOLD == 0, InvalidSignatureLength());

        uint256 sigCount = sigData.length / VerificationLib.SIGNATURE_LENGTH_THRESHOLD;
        bytes32 signedHash = ECDSA.toEthSignedMessageHash(abi.encode(messageHashes));
        address lastValidator = address(0);
        address[] memory recoveredSigners = new address[](sigCount);

        uint256 offset;
        assembly {
            offset := sigData.offset
        }

        for (uint256 i; i < sigCount; i++) {
            (uint8 v, bytes32 r, bytes32 s) = VerificationLib.signatureSplit(offset, i);
            address currentValidator = signedHash.recover(v, r, s);
            require(currentValidator > lastValidator, UnsortedSigners());
            recoveredSigners[i] = currentValidator;
            lastValidator = currentValidator;
        }

        return recoveredSigners;
    }

    function _countBaseSigners(address[] memory signers) private view returns (uint256) {
        uint256 count;

        for (uint256 i; i < signers.length; i++) {
            if (VerificationLib.isBaseValidator(signers[i])) {
                unchecked {
                    count++;
                }
            }
        }

        return count;
    }

    function _countPartnerSigners(IPartner.Signer[] memory partnerValidators, address[] memory signers)
        private
        pure
        returns (uint256)
    {
        uint256 count;
        uint256 signedBitMap;

        for (uint256 i; i < signers.length; i++) {
            uint256 partnerIndex = _indexOf(partnerValidators, signers[i]);
            if (partnerIndex == partnerValidators.length) {
                continue;
            }

            if (signedBitMap & (_BIT << partnerIndex) != 0) {
                revert DuplicateSigner();
            }

            signedBitMap |= _BIT << partnerIndex;
            unchecked {
                count++;
            }
        }

        return count;
    }

    /// @dev Linear search for `addr` in memory array `addrs`.
    function _indexOf(IPartner.Signer[] memory addrs, address addr) private pure returns (uint256) {
        for (uint256 i; i < addrs.length; i++) {
            if (addr == addrs[i].evmAddress || addr == addrs[i].newEvmAddress) {
                return i;
            }
        }
        return addrs.length;
    }
}
